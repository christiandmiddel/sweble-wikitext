/**
 * Copyright 2011 The Open Source Research Group,
 *                University of Erlangen-NÃ¼rnberg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/*
 * Content
 * -------
 *
 *   Overview:
 *     - First see if it's something special (a link, a page switch, ...)
 *     - If not, it's just text
 *
 *   Closer look:
 *     - Special things always have their unique prefix.
 *     - If such a prefix is encountered in the text, the rule for the
 *       corresponding thing MUST match. Either because we really found
 *       something special, or because the rule just consumed the prefix as
 *       plain text to let the parser continue with it's work.
 *
 *   Even closer look:
 *     - This "must match" requirement helps us escape the content if a prefix
 *       of a closing element is encountered in the text and this prefix is also
 *       the prefix of a one of these special things.
 *
 */

module org.sweble.wikitext.parser.parser.Content;

import org.sweble.wikitext.parser.utils.ContentUtils;
import org.sweble.wikitext.parser.utils.Roundtrip;
import org.sweble.wikitext.parser.utils.XmlReference;

import org.sweble.wikitext.parser.parser.ExternalLink;
import org.sweble.wikitext.parser.parser.InternalLink;
import org.sweble.wikitext.parser.parser.PageSwitch;
import org.sweble.wikitext.parser.utils.ParserEntity;
import org.sweble.wikitext.parser.parser.Signature;
import org.sweble.wikitext.parser.parser.Ticks;
import org.sweble.wikitext.parser.parser.Url;
import org.sweble.wikitext.parser.parser.HorizontalRule;
import org.sweble.wikitext.parser.parser.LangConvTag;
import org.sweble.wikitext.parser.parser.List;
import org.sweble.wikitext.parser.parser.SemiPre;
import org.sweble.wikitext.parser.parser.Section;
import org.sweble.wikitext.parser.parser.Table;
import org.sweble.wikitext.parser.parser.TableHeader;
import org.sweble.wikitext.parser.parser.TableCell;
import org.sweble.wikitext.parser.parser.XmlElement;
import org.sweble.wikitext.parser.parser.Whitespace;




// -- Body ---------------------------------------------------------------------

header
{
  import java.util.Iterator;
}

body
{
  private WtNode makeP(WtNodeList pre, WtNodeList p, WtNodeList post)
  {
    WtNodeList l = null;
    if (pre != null)
    {
      int i = 0;
      for (WtNode n : pre)
      {
        if (n.getNodeType() == WtNode.NT_NEWLINE)
        {
          ++i;
          if (i > 2)
            break;
        }
      }

      if (i > 2)
      {
        l = nf.list();

        i = 0;
        for (WtNode n : pre)
        {
          if (n.getNodeType() == WtNode.NT_NEWLINE)
          {
            ++i;
            if (i == 2)
            {
              l.add(n);
              l.add(IntermediateTags.PARAGRAPH.createOpen(nf, false));
            }
            else
              l.add(n);
          }
          else
            l.add(n);
        }

        if (p == null && post == null)
          return l;
      }
      else if (p == null && post == null)
      {
        return pre;
      }
      else
      {
        l = nf.list();
        l.add(pre);
        l.add(IntermediateTags.PARAGRAPH.createOpen(nf, false));
      }
    }
    else
    {
      l = nf.list();
      l.add(IntermediateTags.PARAGRAPH.createOpen(nf, false));
    }

    // p != null
    // l != null

    l.add(p);

    int i = 0;
    if (post != null)
    {
      for (WtNode n : post)
      {
        if (n.getNodeType() == WtNode.NT_NEWLINE)
          ++i;
      }
    }

    if (i > 2)
    {
      for (WtNode n : post)
      {
        if (n.getNodeType() == WtNode.NT_NEWLINE)
        {
          --i;
          if  (i == 2)
          {
            l.add(n);
            l.add(IntermediateTags.PARAGRAPH.createClose(nf, false));
            continue;
          }
        }

        l.add(n);
      }
    }
    else
    {
      l.add(IntermediateTags.PARAGRAPH.createClose(nf, false));
      l.add(post);
    }

    return l;
  }
}




// -- Block content ------------------------------------------------------------

/* BolBlockContent = ( LineStartProd / Paragraph )*
 */

noinline transient WtNodeList BolBlockContent =
  content:Block*
  {
    yyValue = nf.list(content);
  }
;

/* BlockContent = Paragraph ( LineStartProd / Paragraph )*
 */

noinline transient WtNodeList BlockContent =
  head:Paragraph tail:Block*
  {
    yyValue = nf.list(head, tail);
  }
;

private inline WtNode Block =
  !BlockStopper ( LineStartProd / Paragraph )
;

inline void BlockStopper =
    BlockStopperNextSection
  / BlockStopperNextTableElement
;

private noinline transient WtNode LineStartProd =
    &( pExtSpaceStar "{|"   ) Table
  / &( pTpStar       "---"  ) HorizontalRule
  / &( pTpStar       [*#:;] ) List
  / &( pTpStar       [ ]    ) SemiPre
  / &( pTpStar       [=]    ) Sections
;

noinline transient void LooksLikeALineStartProd =
    pExtSpaceStar "{|"
  / pTpStar       "---"
  / pTpStar       [*#:;]
  / pTpStar       [ ]
  / pTpStar       [=]
;




// -- Paragraph --[ State Aware Memoization ]-----------------------------------

noinline transient WtNode Paragraph =
 ^{
    StateAwareResult r = (StateAwareResult) pParagraphMemoized(yyBase);
    final WikitextParserContext context = getContext();
    Result yyResult = r.getResult(context);
    if (yyResult == null)
      yyResult = r.setResult(context, pParagraphTransient(yyBase));
    if (returnTrue(r))
      return yyResult;
  }
;

noinline memoized WtNode ParagraphMemoized =
 ^{
    Result yyResult = new StateAwareResult("Paragraph", getContext(), pParagraphTransient(yyBase));
    if (returnTrue(yyResult))
      return yyResult;
  }
;




// -- Paragraph ----------------------------------------------------------------

/* Paragraph = PreParaWs ParagraphText PostParaWs?
 *           / PreParaWs
 *           / ParagraphText PostParaWs?
 */

noinline transient WtNode ParagraphTransient =
    pre:PreParaWs !ParagraphStopper p:ParagraphText post:PostParaWs?
    {
      yyValue = makeP(pre, (WtNodeList) p, post);
    }
  / PreParaWs
    {
      yyValue = makeP((WtNodeList) yyResult.semanticValue(), null, null);
    }
  / p:ParagraphText post:PostParaWs?
    {
      yyValue = makeP(null, (WtNodeList) p, post);
    }
;

private inline void ParagraphStopper =
    BlockStopper
  / LineStartProd
;

/* PreParaWs = S* EOL ( S* EOL )*
 */

private noinline transient WtNodeList PreParaWs =
  s:pExtSpaceStar lt:pEolNode tail:PreParaWsMore*
  {
    yyValue = nf.list(s, lt, nf.list(tail));
  }
;

private inline WtNodeList PreParaWsMore =
  !ParagraphStopper s:pExtSpaceStar lt:pEolNode
  {
    yyValue = nf.list(s, lt);
  }
;

/* ParagraphText = Content+ ( EOL Content+ )*
 */

private noinline transient WtNode ParagraphText =
  head:InlineContentPlus tail:ParagraphTextMore*
  {
    yyValue = nf.list(head, tail);
  }
;

private inline WtNode ParagraphTextMore =
  lt:pEolNode !ParagraphStopper s:pExtSpaceStar c:InlineContentPlus
  {
    yyValue = nf.list(lt, s, c);
  }
;

/* PostParaWs = EOL ( S* EOL )*
 */

private noinline transient WtNodeList PostParaWs =
  lt:pEolNode tail:PostParaWsMore* ( &ParagraphStopper / !ParagraphText )
  {
    yyValue = nf.list(lt, tail);
  }
;

private inline WtNodeList PostParaWsMore =
  !ParagraphStopper s:pExtSpaceStar lt:pEolNode
  {
    yyValue = nf.list(s, lt);
  }
;




// -- For strange things like internal link titles -----------------------------

/* InlineBlockContent = InlineTextBlock ( LineStartProd / InlineTextBlock )
 */

noinline transient WtNodeList InlineBlockContent =
  head:( !BlockStopper InlineTextBlock )? tail:InlineBlock*
  {
    yyValue = nf.list(head, nf.list(tail));
  }
;

private inline WtNode InlineBlock =
  !BlockStopper ( LineStartProd / InlineTextBlock )
;

/* InlineTextBlock = Content+ ( EOL Content* )*
 *                 /          ( EOL Content* )+
 */

private transient WtNode InlineTextBlock =
    head:InlineContentPlus tail:InlineTextBlockMore*
    {
      yyValue = nf.list(head, tail);
    }
  / tail:InlineTextBlockMore+
    {
      yyValue = nf.list(tail);
    }
;

private inline WtNode InlineTextBlockMore =
  lt:pEol more:( !ParagraphStopper InlineContentPlus )?
  {
    yyValue = nf.list(nf.text(lt), more);
  }
;




// -- Inline Content -----------------------------------------------------------

noinline transient WtNodeList InlineContentStar =
 ^{
    StateAwareResult r = (StateAwareResult) pInlineContentStarMemoized(yyBase);
    final WikitextParserContext context = getContext();
    Result yyResult = r.getResult(context);
    if (yyResult == null)
      yyResult = r.setResult(context, pInlineContentStarTransient(yyBase));
    if (returnTrue(r))
      return yyResult;
  }
;

noinline memoized WtNodeList InlineContentStarMemoized =
 ^{
    Result yyResult = new StateAwareResult("InlineContentStar", getContext(), pInlineContentStarTransient(yyBase));
    if (returnTrue(yyResult))
      return yyResult;
  }
;

noinline transient WtNodeList InlineContentStarTransient =
  content:InlineContentChoice*
  {
    yyValue = nf.list(content);
  }
;

noinline transient WtNodeList InlineContentPlus =
 ^{
    StateAwareResult r = (StateAwareResult) pInlineContentPlusMemoized(yyBase);
    final WikitextParserContext context = getContext();
    Result yyResult = r.getResult(context);
    if (yyResult == null)
      yyResult = r.setResult(context, pInlineContentPlusTransient(yyBase));
    if (returnTrue(r))
      return yyResult;
  }
;

noinline memoized WtNodeList InlineContentPlusMemoized =
 ^{
    Result yyResult = new StateAwareResult("InlineContentPlus", getContext(), pInlineContentPlusTransient(yyBase));
    if (returnTrue(yyResult))
      return yyResult;
  }
;

noinline transient WtNodeList InlineContentPlusTransient =
  content:InlineContentChoice+
  {
    yyValue = nf.list(content);
  }
;

/* TextPlus will stop on ContentAtoms and ContentStoppers.
 *
 * ContentAtoms will never fail if there is a ContentAtom prefix. If it's not a
 * real ContentAtom or the respective ContentAtom is not allowed in a given
 * context the production responsible for the prefix will consume a part of the
 * prefix and return a WtText node containing only the consumed prefix. That's
 * why ContentAtoms will never fail if there is a ContentAtom prefix.
 *
 * Eventually this production will only fail, if it encounters a
 * ContentStopper, that is a syntactic element which is important to the
 * surrounding production and must therefore not be consumed as part of the
 * content.
 *
 * Plain URLs are quite the hack (for performance reasons; they don't have a
 * "proper prefix") and have to be treated specially (see Url grammar for
 * details). The same goes for internal links and their prefix.
 */

private transient WtNode InlineContentChoice =
    text:TextPlus ':' &{ accept(ParserAtoms.PLAIN_EXTERNAL_LINK) } path:UrlPathString &{ isProtocol(text, path) }
    {
      yyValue = makeExternalLink(text, path);
    }
  / text:TextPlus &"[[" !InlineContentStopper link:InternalLink
    {
      yyValue = link.isNodeType(WtNode.NT_INTERNAL_LINK) ?
          addLinkPrefix(text, (WtInternalLink) link) :
          nf.list(text, link);
    }
  / TextPlus
  / !InlineContentStopper InlineContentAtom
  / !InlineContentStopper text:TextStopperPrefix
    {
      yyValue = nf.text(text);
    }
;


/* The predicates before each ContentAtom are only there to simplify debugging.
 * And sort of to remind us which production is responsible for which prefix.
 */

private inline WtNode InlineContentAtom =
    &"~~"     Signature
  / &"[["     InternalLink
  / &"["      ExternalLink
  / &"''"     Ticks
  / &"__"     PageSwitch
  / &"<"      XmlElement
  / &"&"      XmlReference
  / &"-{"     LanguageConversionTag
  / &"\uE000" ParserEntity
;

private inline void InlineContentStopper =
    slEol
  / InlineContentStopperExternalLink
  / InlineContentStopperInternalLink
  / InlineContentStopperHeading
  / InlineContentStopperTableHeader
  / InlineContentStopperTableCell
  / InlineContentStopperLangConvTag
;

/* Plain text is basically everything that is not a ContentAtom (semantically,
 * a WtText node is also a ContentAtom, however, it is not part of the respective
 * production). Also plain text has to stop at so-called TextStoppers. These
 * are syntactic elements which are significant to the surrounding production
 * and must not become part of the plain text (in order for the surrounding
 * production to see and consume them).
 */

private memoized WtText TextPlus =
  text:TextPlusStr
  {
    yyValue = nf.text(text);
  }
;

private transient String TextPlusStr = ( !TextStopperPrefix _ )+ ;

private inline String TextStopperPrefix =

  // -- stop characters --

    pSlEol          // block level end (lists, semi pre)
                    // heading abort
                    // external link title abort
                    // table element start
                    // table cell/header inline element end
  / "="             // heading end
  //"=>"            // LCT (already caught by "heading end"
  / "|"             // internal link parameter end
                    // table cell inline element end
                    // LCT
  / "!"             // table header inline element end
  / "]"             // external and internal link title end
  / ":"             // Definition list definition (in the pair TERM:DEF)
                    // LCT
  / ";"             // LCT
  / "}-"            // LCT end

  // -- content atom prefix --

  / ":"             // plain url
  / "~~"            // signature
  / "["             // internal or external link
  / "''"            // bold or italic
  / "__"            // page switch
  / "<"             // xml tag and comment
  / "&"             // xml entity
  / "-{"            // LCT start
  / "\uE000"        // parser entity
;




// -- End of file --------------------------------------------------------------
